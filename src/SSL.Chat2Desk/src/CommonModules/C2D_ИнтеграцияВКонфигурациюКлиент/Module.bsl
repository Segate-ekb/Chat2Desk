#Область ПрограммныйИнтерфейс

Процедура ОтослатьУведомлениеЧерезChat2Desk(Ссылка, ПараметрыВыполнения) Экспорт
	
	ДополнительныеПараметры = новый Структура("Ссылка, Шаблон",  Ссылка);
	ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаКомандыПослеВыбораШаблона", ЭтотОбъект, ДополнительныеПараметры);
	ИмяТипаДанных = Лев(ПараметрыВыполнения.Форма.ИмяФормы, СтрНайти(ПараметрыВыполнения.Форма.ИмяФормы,".",,,2)-1);
	ЗначениеОтбора = Новый Структура("ПолноеИмяТипаПараметраВводаНаОсновании, ПредназначенДляChat2Desk", ИмяТипаДанных, истина);
	ПараметрыВыбора = Новый Структура("Отбор", ЗначениеОтбора);
	ОткрытьФорму("Справочник.C2D_ШаблоныСообщений.ФормаВыбора",ПараметрыВыбора,ПараметрыВыполнения.Форма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработкаКомандыПослеВыбораШаблона(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Шаблон", ВыбранноеЗначение);
	КонецЕсли;
	
	//Здесь можно дописать обработку поиска шаблонов и транспортов по умолчанию. Но Пока просто заглушка	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.Шаблон) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось определить параметры отправки уведомления.(Шаблон и предпочтительный способ связи.)";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли; 	
	
	Попытка
		C2D_ИнтеграцияВКонфигурациюВызовСервера.ОтправитьУведомлениеНаСервере(ДополнительныеПараметры.Шаблон, ДополнительныеПараметры.Ссылка);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Уведомление о "+Строка(ДополнительныеПараметры.Ссылка)+" Успешно создано."; 
		Сообщение.Сообщить();																							
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "При отправке уведомления произошла ошибка:
		|
		|"+КраткоеПредставлениеОшибки(ИнформацияОбОшибке())+Символы.ПС+"Обратитесь к администратору.";
		Сообщение.Сообщить();
	КонецПопытки;																							
КонецПроцедуры

#КонецОбласти