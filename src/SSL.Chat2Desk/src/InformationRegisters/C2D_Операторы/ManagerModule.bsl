#Область ПрограммныйИнтерфейс

Процедура ОбновитьСписокОператоровВФоне(ДополнительныеПараметры, адресВременногоХранилища) Экспорт
	ОбновитьСписокОператоров();		
КонецПроцедуры

Процедура ОбновитьСписокОператоров() Экспорт
	//Считаем, что операторов не может быть много, потому будем их обновлять в синхронном режиме.
	Офсет = 0;
	Лимит = 100;
	Тотал = 999;  //Первый прогон цикла
	Пока офсет+лимит < Тотал цикл
		СтруктураПараметров = Новый структура("offset, limit", Офсет, лимит);	
		ОтветАпи = C2D_ВзаимодействиеСApi.operators(СтруктураПараметров);
		Для каждого ДанныеОператора Из ОтветАпи.Получить("data") Цикл
			ОбновитьДанныеОператораПоСоответствию(ДанныеОператора);			
		КонецЦикла;
		
		//Если не вошло в пачку, то запросим следующую порцию 
		Тотал = ОтветАпи.получить("meta").получить("total");
		Офсет = Офсет+Лимит;
	КонецЦикла;	
	
КонецПроцедуры

Функция НайтиСсылкуПоID(id) экспорт
	УстановитьПривилегированныйРежим(Истина);
	Пользователь = неопределено;
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	C2D_МэппингID.Пользователь КАК Пользователь
	               |ИЗ
	               |	РегистрСведений.C2D_МэппингID КАК C2D_МэппингID
	               |ГДЕ
	               |	C2D_МэппингID.ID = &ID";
	Запрос.УстановитьПараметр("ID", id);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Пользователь = Выборка.Пользователь;
	КонецЕсли; 
	Возврат Пользователь;
КонецФункции

Функция НайтиIDПоСсылке(Ссылка) Экспорт
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	C2D_Операторы.ID КАК ID
	               |ИЗ
	               |	РегистрСведений.C2D_Операторы КАК C2D_Операторы
	               |ГДЕ
	               |	C2D_Операторы.Пользователь = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ID");
	
КонецФункции

Функция СформироватьСписокОператоров(ТолькоОнлайн = ложь) Экспорт
	СписокОператоров = новый СписокЗначений;
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РегистрСведенийC2D_Операторы.ID КАК ID,
	               |	ВЫБОР
	               |		КОГДА РегистрСведенийC2D_Операторы.Пользователь = ЗНАЧЕНИЕ(Справочник.пользователи.пустаяссылка)
	               |			ТОГДА ""email: "" + РегистрСведенийC2D_Операторы.email + "" ("" + РегистрСведенийC2D_Операторы.first_name + "" "" + РегистрСведенийC2D_Операторы.last_name + "")""
	               |		ИНАЧЕ РегистрСведенийC2D_Операторы.Пользователь
	               |	КОНЕЦ КАК Представление
	               |ИЗ
	               |	РегистрСведений.C2D_Операторы КАК РегистрСведенийC2D_Операторы";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокОператоров.Добавить(Выборка.id, Выборка.Представление);	
	КонецЦикла;
	
	Возврат СписокОператоров;
КонецФункции

Функция ПолучитьТелоДанныхАвторизации(Пользователь) экспорт
	СтрокаРезультат = "";
	Запрос = новый запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	C2D_Операторы.email КАК email,
	               |	C2D_Операторы.password КАК password
	               |ИЗ
	               |	РегистрСведений.C2D_Операторы КАК C2D_Операторы
	               |ГДЕ
	               |	C2D_Операторы.Пользователь = &Пользователь";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтрокаРезультат = СтрШаблон("{""email"":""%1"",""password"":""%2""}",выборка.email, выборка.password);
	КонецЕсли; 
	Возврат СтрокаРезультат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ОбновитьДанныеОператораПоСоответствию(Соответствие)
 	Запись = РегистрыСведений.C2D_Операторы.СоздатьМенеджерЗаписи();
	Запись.ID = Соответствие.Получить("id");
	Запись.Прочитать();
	Если Запись.Выбран() = Ложь Тогда
		Запись.ID = Соответствие.Получить("id");
	КонецЕсли;

	Запись.first_name = Соответствие.получить("first_name");
	Запись.last_name = Соответствие.получить("last_name");
	Запись.email = Соответствие.получить("email");
	Запись.phone = Соответствие.получить("phone");
	Запись.avatar = Соответствие.получить("avatar");
	Запись.Записать();
КонецПроцедуры
 
#КонецОбласти