#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СопоставленоОтбор = "Все";
	ТипыДанных = РегистрыСведений.C2D_ТипыКлиентов.ПолучитьТипыДанных();
	Если не ЗначениеЗаполнено(ТипыДанных) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнены типы привязки клиентов chat2desk к базе 1с";
		Сообщение.Сообщить();
		Отказ = истина;
		Возврат;
	КонецЕсли; 
	Элементы.КлиентТекущейСтроки.ОграничениеТипа = ТипыДанных;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	&НаКлиенте
Процедура КлиентТекущейСтрокиПриИзменении(Элемент)
	ОбновитьКлиента(Элементы.список.текущиеДанные.id,КлиентТекущейСтроки);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура КлиентТекущейСтрокиОчистка(Элемент, СтандартнаяОбработка)
	ОбновитьКлиента(Элементы.список.текущиеДанные.id,КлиентТекущейСтроки);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура СопоставленоОтборПриИзменении(Элемент)
		ОбработатьОтборЗаписейСопоставления();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	КлиентТекущейСтроки = НайтиКлиентаСервер(ТекущиеДанные.Клиент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОбновитьДанныеПоКлиентам(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопросОбОбновлении", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Обнолвение данных по клиентам может занять длительное время. Продолжить?",РежимДиалогаВопрос.ОКОтмена, 60);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоКИ(Команда)
	ЗаполнитьПоКИНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура ОбновитьКлиента(id, Клиент)
	Запись = РегистрыСведений.C2D_Клиенты.СоздатьМенеджерЗаписи();
	Запись.id = id;
	Запись.Прочитать();
	Если не Запись.Клиент = Клиент Тогда
		Если не ЗначениеЗаполнено(Клиент) Тогда
			запись.Клиент = неопределено;
			Запись.ПолноеИмяТипаДанных = "";
		Иначе
			запись.Клиент = Клиент.УникальныйИдентификатор();
			Запись.ПолноеИмяТипаДанных =  клиент.Метаданные().полноеИмя();
		КонецЕсли;
		Запись.Представление = РегистрыСведений.C2D_Клиенты.СформироватьПредставление(Запись);
		Запись.Записать();	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОбОбновлении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если не РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		возврат;
	КонецЕсли; 
	ДлительнаяОперация = НачатьОбновлениеКлиентовВФоне();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ПослеЗавершенияОбновленияКлиентов",ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияОбновленияКлиентов(Результат, ДополнительныеПараметры) Экспорт
	Элементы.Список.Обновить();
КонецПроцедуры


&НаСервере
Функция НачатьОбновлениеКлиентовВФоне()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьВФоне("РегистрыСведений.C2D_Клиенты.ОбновитьСписокКлиентовВФоне", , ПараметрыВыполнения);
КонецФункции


&НаСервере
Процедура ОбработатьОтборЗаписейСопоставления()
	
	Если СопоставленоОтбор = "Сопоставленные" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(список, "Клиент",,ВидСравненияКомпоновкиДанных.Заполнено,,Истина);
	ИначеЕсли СопоставленоОтбор = "Несопоставленные" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(список, "Клиент",,ВидСравненияКомпоновкиДанных.НеЗаполнено,,Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(список, "Клиент",,,,Ложь);
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ЗаполнитьПоКИНаСервере()
	РегистрыСведений.C2D_Клиенты.выполнитьПоискКлиентовПоКИВБазе();
	Элементы.Список.Обновить();	
КонецПроцедуры

&НаСервере
Функция НайтиКлиентаСервер(Guid)
	Возврат РегистрыСведений.C2D_Клиенты.НайтиСсылкуПоGUID(Guid);
КонецФункции

#КонецОбласти