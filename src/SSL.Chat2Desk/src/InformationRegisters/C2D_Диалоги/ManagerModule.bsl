#Если сервер или ВнешнееСоединение тогда
#Область ПрограммныйИнтерфейс

Процедура СоздатьИлиОткрытьДиалог(Соостветствие) Экспорт 
	Запись = РегистрыСведений.C2D_Диалоги.СоздатьМенеджерЗаписи();
	Запись.id = Соостветствие.Получить("dialog_id");
	Запись.Прочитать();
	Если не Запись.Выбран() Тогда
		Запись.id = Соостветствие.Получить("dialog_id");
	КонецЕсли;
	Запись.client_id = Соостветствие.Получить("client_id");
	//«Правильно ли я понимаю, что если мы получаем хук new_request мы всегда проставляем null оператору?»
	//— Если на аккаунте не влючено автоматическое назначение, то да
	//обнуляем оператора при переоткрытии реквеста
	//Запись.operator_id = 0;
	Запись.state = "open";
	Запись.transport = Соостветствие.Получить("transport");
	Запись.Записать();
	
	РегистрыСведений.C2D_ТранспортыКлиентов.ДобавитьТранспортКлиенту(Запись.client_id, Запись.transport);
КонецПроцедуры

Процедура ПеренестиДиалогДругомуОператору(Соостветствие) экспорт

	Запись = РегистрыСведений.C2D_Диалоги.СоздатьМенеджерЗаписи();
	Запись.id = Соостветствие.Получить("dialog_id");
	Запись.Прочитать();
	Если не Запись.Выбран() Тогда
		Запись.id = Соостветствие.Получить("dialog_id");
	КонецЕсли;
	Если Запись.operator_id = Соостветствие.Получить("operator_id") Тогда
		 Возврат;
	КонецЕсли; 
	Запись.operator_id = Соостветствие.Получить("operator_id");
	Запись.Записать();
	
	РегистрыСведений.C2D_ОповещенияОператоров.ОповеститьОператораОДиалоге(Запись.operator_id, Запись.id, истина);
	РегистрыСведений.C2D_ОповещенияОператоров.ОчиститьНеактуальныеОповещенияПриСменеОператора(Запись.id);

КонецПроцедуры

Процедура ЗакрытьДиалог(Соостветствие) экспорт
	Запись = РегистрыСведений.C2D_Диалоги.СоздатьМенеджерЗаписи();
	Запись.id = Соостветствие.Получить("dialog_id");
	Запись.Прочитать();
	Если не Запись.Выбран() Тогда
		ВызватьИсключение "Нет записи для закрытия диалога!";
	КонецЕсли;
	Запись.state = "closed";
	Запись.Записать();
	
КонецПроцедуры

Процедура ОбновитьСписокДиалоговВФоне(ДополнительныеПараметры, адресВременногоХранилища) Экспорт
	ОбновитьСписокДиалогов();		
КонецПроцедуры

//Полное обновление - это либо первоначальное заполнение, либо внеплановое действие. так что оно не должно быть часто.
Процедура ОбновитьСписокДиалогов() Экспорт
	Офсет = 0;
	Лимит = 200;
	Тотал = 999;  //Первый прогон цикла
	Пока офсет+лимит < Тотал цикл
		СтруктураПараметров = Новый структура("offset, limit", Офсет, лимит);	
		ОтветАпи = C2D_ВзаимодействиеСApi.GET_dialogs(СтруктураПараметров);
		Для каждого ДанныеДиалога Из ОтветАпи.Получить("data") Цикл
			ОбновитьДанныеДиалогаПоСоответствию(ДанныеДиалога);			
		КонецЦикла;
		
		//Если не вошло в пачку, то запросим следующую порцию 
		Тотал = ОтветАпи.получить("meta").получить("total");
		Офсет = Офсет+Лимит;
	КонецЦикла;	
	
КонецПроцедуры

Функция  ПередатьЧатДругомуОператору(dialog_id, operator_id) Экспорт
	СтруктураПараметров = Новый структура("operator_id", operator_id);
	СоответствиеОтвет = C2D_ВзаимодействиеСApi.PUT_dialogs(dialog_id, СтруктураПараметров);
	
	Если СоответствиеОтвет.Получить("status") = "success" Тогда
		Возврат истина;
	ИначеЕсли СоответствиеОтвет.Получить("status") = "error" тогда
		Сообщение = Новый СообщениеПользователю;  			
	    Сообщение.Текст = C2D_ВзаимодействиеСApiСлужебный.СформироватьТекстОшибки(СоответствиеОтвет);
		Сообщение.Сообщить();
		Возврат ложь;
	КонецЕсли;
КонецФункции
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ОбновитьДанныеДиалогаПоСоответствию(Соответствие)
	ПоследнееСообщение =Соответствие.Получить("last_message"); 
	Запись = РегистрыСведений.C2D_Диалоги.СоздатьМенеджерЗаписи();
	
	Запись.ID = Соответствие.Получить("id");
	Запись.state = Соответствие.получить("state");
	Запись.operator_id = Соответствие.получить("operator_id");
	Запись.client_id = ПоследнееСообщение.получить("client_id");
	Запись.transport = ПоследнееСообщение.получить("transport");
	Запись.Записать();
	РегистрыСведений.C2D_Сообщения.ЗаписатьИнформациюОНовомСообщении(ПоследнееСообщение);
	РегистрыСведений.C2D_ТранспортыКлиентов.ДобавитьТранспортКлиенту(Запись.client_id, Запись.transport);
КонецПроцедуры

#КонецОбласти
#КонецЕсли