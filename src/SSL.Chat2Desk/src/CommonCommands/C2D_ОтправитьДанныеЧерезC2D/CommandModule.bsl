&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ДополнительныеПараметры = новый Структура("Ссылка, Шаблон",  ПараметрКоманды);
	

	ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаКомандыПослеВыбораШаблона", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВводЗначения(ОписаниеОповещения, ДополнительныеПараметры.Шаблон, "Выберите шаблон сообщения!", Тип("СправочникСсылка.C2D_ШаблоныСообщений"));
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыПослеВыбораШаблона(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("Шаблон", ВыбранноеЗначение);
	КонецЕсли;
	
	//Здесь можно дописать обработку поиска шаблонов и транспортов по умолчанию. Но Пока просто заглушка	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.Шаблон) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось определить параметры отправки уведомления.(Шаблон и предпочтительный способ связи.)";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли; 	
	
	Попытка
		ОтправитьУведомлениеНаСервере(ДополнительныеПараметры);
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Уведомление о "+Строка(ДополнительныеПараметры.Ссылка)+" Успешно создано."; 
		Сообщение.Сообщить();																							
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "При отправке уведомления произошла ошибка:
		|
		|"+КраткоеПредставлениеОшибки(ИнформацияОбОшибке())+Символы.ПС+"Обратитесь к администратору.";
		Сообщение.Сообщить();
	КонецПопытки;																							
КонецПроцедуры

&НаСервере
Процедура ОтправитьУведомлениеНаСервере(ДополнительныеПараметры)
	Результат = C2D_ШаблоныСообщений.СформироватьСообщениеИОтправить(ДополнительныеПараметры.Шаблон, ДополнительныеПараметры.Ссылка, новый УникальныйИдентификатор); 	
	Если Результат.Отправлено = ложь Тогда
		ВызватьИсключение результат.ОписаниеОшибки;
	КонецЕсли; 
КонецПроцедуры
